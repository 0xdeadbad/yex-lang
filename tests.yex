def assert(condition, message) do
	if !condition then
		raise(@AssertionError, message)
		exit(1)
	end
end

let ops_tests = [
	1 + 2 == 3,
	1 - 2 == -1,
	1 * 2 == 2,
	1 / 2 == 0.5,
	3 % 2 == 1,
	1 <<< 2 == 4,
	1 >>> 2 == 0,
	1 &&& 2 == 0,
	1 ||| 2 == 3,
	1 ^^^ 2 == 3,
	1 == 1,
	1 != 2,
	1 < 2,
	1 <= 1,
	3 > 2,
	3 >= 3,
	1 && 2,
	1 || 0,
]

ops_tests:map(fn(test) do
	assert(test, "Operator test failed")
end)

class Person(name, age)
	def init(this) do
		assert(this.name is Str, "Name must be a string")
		assert(this.age is Num, "Age must be a number")
		this
	end

	def adult?(this): this.age >= 18
end

let p = new Person("John", 18)
assert(p:adult?(), "Person is not adult")
assert(p is Person, "p is not a Person")

assert(Person#adult?(p), "Static method Person#adult? failed")

try
	raise(@AssertionError, "should be rescued")
rescue e
	assert(e == @AssertionError, "Should be an assertion error")
end

assert(
	[1, 2, 3]
		:map(fn(i): i * 2)
		:map(fn(i): i + 1)
		:filter(fn(i): i != 3)
		:fold(0, fn(acc, i): acc + i)
			== 12,
	"List tests failed",
)

def tailcall(counter, _) do
	when counter do
		else _ if counter > 0 =>
			->tailcall(counter - 1, _)
		0 => "Done"
	end
end

assert(tailcall(10, nil) == "Done", "Tailcall test failed")

println("All tests passed")
